// Top-level build file where you can add configuration options common to all sub-projects/modules.
// external 全局变量
ext{
    compileSdkVersion = 24
    buildToolsVersion = '24.0.1'

    minSdkVersion = 15
    targetSdkVersion = 24
    versionCode = 1
    versionName = "1.0"

//    kotlin_version = "1.1.3-2"

    supportLibsVersion = '24.2.0'

    // App dependencies
    supportLibraryVersion = "25.3.1"

    rxjavaVersion = "2.1.0"
    rxandroidVersion = "2.0.1"
    rxpermissionsVersion = "0.9.4@aar"
    rxlifecycleVersion = "2.1.0"

    okhttp3Version = "3.8.0"
    retrofitVersion = "2.3.0"
    retrofit2rxjava2Version = "1.0.0"
    klogVersion = "1.6.0"

    leakcanaryVersion = "1.5.1"

    glideVersion = "3.7.0"
    glideTransformationsVersion = "2.0.1"
    glideOkhttp3IntegrationVersion = "1.4.0@aar"

    eventbusVersion = "2.4.0"
    swipebackhelperVersion = "3.1.2"
}

buildscript {
    ext.kotlin_version = "1.1.3-2"
    ext.android_plugin_version = "2.2.0"

    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    def isAppModule = it.name.equals('app')
    def enableDataBinding = isAppModule || it.name.equals('databinding')

    apply plugin: isAppModule? 'com.android.application': 'com.android.library'

    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'   // 拓展

    if(enableDataBinding)
        apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion rootProject.ext.compileSdkVersion as Integer
        buildToolsVersion rootProject.ext.buildToolsVersion as String

        defaultConfig {
            if(isAppModule)
                applicationId "com.fashare.mvvm_juejin"

            minSdkVersion rootProject.ext.minSdkVersion as Integer
            targetSdkVersion rootProject.ext.targetSdkVersion as Integer
            versionCode rootProject.ext.versionCode as Integer
            versionName rootProject.ext.versionName as String
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
        }

        if(enableDataBinding){
            dataBinding {
                enabled true
            }
        }
    }

    dependencies {
        // kotlin
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        /******* rx ****************************************************************************/
        compile "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxjavaVersion"    //1.3MB [EventBus]
        compile "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxandroidVersion" //8KB

        if(enableDataBinding){
            kapt "com.android.databinding:compiler:$android_plugin_version"

            compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter:1.1.0'
            compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter-recyclerview:1.1.0'
        }
    }
}
